plugins {
    id 'java'
    id 'antlr'
}

group = 'com.levelrin.chromescript'
version = '0.0.1'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            // We will generate the parser in this directory using Antlr.
            // We need to add this directory to the main source set to use the generated classes in our code.
            srcDirs("$buildDir/generated-src/antlr/main")
        }
    }
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.2'
    implementation 'commons-cli:commons-cli:1.11.0'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

generateGrammarSource {
    maxHeapSize = '64m'
    arguments += [
        '-visitor',
        '-long-messages'
    ]
    // Since we will add the package declaration, we must put generated classes in the package folder.
    outputDirectory = file("$buildDir/generated-src/antlr/main/com/levelrin/antlr/generated")
}

jar {
    manifest {
        attributes 'Main-Class': 'com.levelrin.chromescript.Main'
    }
    // Fat jar configuration.
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveFileName = "${project.name}-${project.version}-java${JavaVersion.current().toString()}.jar"
}
